plugins {
    id 'groovy'
}

wrapper {
    gradleVersion = '8.7'
}

repositories {
    mavenCentral()
}

group = 'com.example.spock'
version = '1.0-SNAPSHOT'

def allureVersion = '2.26.0'
def aspectJVersion = '1.9.22'
def groovyVersion = '4.0.20'
def spockVersion = '2.3-groovy-4.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {
    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation "io.qameta.allure:allure-spock2"
    
    // it is important to have spock dependencies to be defined after allure-spock2!!!
    // see https://github.com/allure-framework/allure-java/issues/928#issuecomment-1594596412
    testImplementation platform("org.spockframework:spock-bom:$spockVersion")
    testImplementation "org.spockframework:spock-core"
    
    testImplementation platform("org.apache.groovy:groovy-bom:$groovyVersion")

    testImplementation platform("org.junit:junit-bom:5.10.2")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    
    testImplementation "org.slf4j:slf4j-simple:2.0.12"
}

test {
    ignoreFailures = true
    useJUnitPlatform()
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
}
